plugins {
    id 'com.android.application'
    id 'org.lsposed.lsparanoid'
}

android {
    namespace 'com.modfs.lstardust'
    compileSdk 32

    defaultConfig {
        ndk {
            abiFilters 'arm64-v8a'
        }
    }


    defaultConfig {
        applicationId "com.modfs.lstardust"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                // flags for obfuscate-llvm17:
                //cppFlags '-std=c++17 -fvisibility=hidden -fvisibility-inlines-hidden -mllvm -icall -mllvm -ibr -mllvm -igv -mllvm -sub -mllvm -sub_loop=3 -mllvm -split -mllvm -split_num=5 -mllvm -bcf_loop=3 -mllvm -sobf -mllvm -bcf_prob=80 -mllvm -bcf -mllvm -fla'
                // flags for sexy hikari (full obfuscate)
                //cppFlags '-std=c++17 -fvisibility=hidden -fvisibility-inlines-hidden -mllvm -enable-bcfobf -mllvm -enable-cffobf -mllvm -enable-splitobf -mllvm -enable-subobf -mllvm -enable-acdobf -mllvm -enable-strcry -mllvm -enable-funcwra -mllvm -enable-fco'
                // flags for mild hikari
                //cppFlags '-std=c++17 -fvisibility=hidden -fvisibility-inlines-hidden -mllvm -enable-strcry -mllvm -enable-acdobf -mllvm -enable-subobf -mllvm -enable-fco -mllvm -enable-funcwra' //  -mllvm -enable-cffobf
                // USE THIS FOR NO OBFUSCATION (fast compile times)
                cppFlags '-std=c++17 -O2 -Wno-null-conversion'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        lsposed {
        }
        lsposedUnsigned {
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
    androidResources {
        noCompress 'so'
    }
    ndkVersion '26.1.10909125'
}

afterEvaluate {

    tasks.configureEach {_task ->
        if(_task.name.contains("assembleRelease")) {
            _task.finalizedBy patchBuild
        }
    }

    tasks.configureEach {_task ->
        if(_task.name.endsWith("assembleLsposedUnsigned")) {
            _task.finalizedBy installXposedModuleUnsigned
        }
    }

    tasks.configureEach { _task ->
        if (_task.name.endsWith("assembleLsposed")) {
            _task.finalizedBy installXposedModule
        }
    }

    task installXposedModule() {
        doLast {
            exec {
                workingDir "${rootDir}"
                commandLine android.adbExecutable, "install", "-r", "-d", "${rootDir}/app/lsposed/app-lsposed.apk"
            }
        }
    }

    task installXposedModuleUnsigned() {
        doLast {
            exec {
                workingDir "${rootDir}"
                commandLine android.adbExecutable, "install", "-r", "-d", "${rootDir}/app/build/outputs/apk/lsposedUnsigned/app-lsposedUnsigned-unsigned.apk"
            }
        }
    }
    
    task installPatch() {
        doLast {
            exec {
                workingDir "${rootDir}"
                commandLine android.adbExecutable, "install", "-r", "-d", "${rootDir}/out/pixelgun-398-lspatched.apk"
            }
        }
    }

    task patchBuild(type: Exec) {
        workingDir "${rootDir}"
        if (System.properties['os.name'].toLowerCase().contains('windows')) {
            commandLine "cmd", "/c", "java -jar ./patch/lspatch.jar -l 2 -m \"./app/build/outputs/apk/release/app-release-unsigned.apk\" -v \"./patch/pixelgun.apk\" -f -o ./out"
            finalizedBy installPatch
        } else {
            commandLine "bash", "-c", "java -jar ./patch/lspatch.jar -l 2 -m \"./app/build/outputs/apk/release/app-release-unsigned.apk\" -v \"./patch/pixelgun.apk\" -f -o ./out"
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    compileOnly 'de.robv.android.xposed:api:82'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}



